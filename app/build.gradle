plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    // By defining it, R and BuildConfig classes are generated in this package
    namespace "com.github.erfansn.gradlepractice"
    testNamespace "com.github.erfansn.gradlepractice.testing"

    compileSdk 32

    defaultConfig {
        applicationId "ir.erfansn.gradlepractice"
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
        staging {
            initWith debug
            applicationIdSuffix ".debugStaging"
        }
    }

    flavorDimensions "api", "mode"
    productFlavors {
        full {
            dimension "mode"
            // AGP automatically add module's flavor to that dimension because only one dimension defined
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
        }
        demo {
            dimension "mode"
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"
        }

        minApi24 {
            dimension "api"
            minSdkVersion 24
            versionCode 30000 + android.defaultConfig.versionCode
            versionNameSuffix "-minApi24"
        }
        minApi23 {
            dimension "api"
            minSdkVersion 23
            versionCode 20000  + android.defaultConfig.versionCode
            versionNameSuffix "-minApi23"
        }
        minApi21 {
            dimension "api"
            minSdkVersion 21
            versionCode 10000  + android.defaultConfig.versionCode
            versionNameSuffix "-minApi21"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle false
    }
}

dependencies {
    implementation project(':mylibrary')
    // implementation 'androidx.core:core-ktx:1.7.0'
    implementation group: 'androidx.core', name: 'core-ktx', version: '1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.2' /*{
        exclude group: 'com.example.imgtools', module: 'native'
    }*/

    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}